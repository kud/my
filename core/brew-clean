#!/usr/bin/env zsh

# Load helper functions and variables
source $MY/core/helper

echo_title "Starting Cleanup Process..."
echo_space

### LOAD CASKS FROM FILES ###

echo_info "Loading casks..."
echo_space

file_paths=("$MY/core/brew" "$MY/core/fonts" "$MY/profiles/$OS_PROFILE/core/brew")
declare -A file_casks_map

for file_path in "${file_paths[@]}"; do
    while read -r line || [[ -n "$line" ]]; do
        line_without_comment=${line%%#*}
        trimmed_line=$(echo "$line_without_comment" | xargs)

        if [[ "$trimmed_line" =~ ^caskinstall\ (.+)$ ]]; then
            cask_name=${match[1]}
            sanitized_cask_name=$(echo "$cask_name" | awk -F/ '{print $NF}')
            file_casks_map[$sanitized_cask_name]=1
        fi
    done <"$file_path"
done

echo_success "Casks loaded"
echo_space

### CHECK INSTALLED CASKS ###

echo_info "Checking installed casks..."
echo_space

installed_casks=$(brew list --cask)
declare -A installed_casks_map
for cask in ${(f)installed_casks}; do
    installed_casks_map[$cask]=1
done

echo_success "Installed casks checked"
echo_space

### COMPARE CASKS ###

echo_info "Comparing casks..."
echo_space

missing_casks=()
for cask in ${(k)file_casks_map}; do
    if [[ -z "${installed_casks_map[$cask]}" ]]; then
        missing_casks+=($cask)
    fi
done

if [[ ${#missing_casks[@]} -gt 0 ]]; then
    echo_info "Some casks are missing"
    for cask in "${missing_casks[@]}"; do
        echo "$cask"
    done
    exit 1
else
    echo_success "All casks are in order"
fi

echo_space

### HANDLE DISCREPANCIES ###

echo_info "Checking for discrepancies..."
echo_space

casks_to_keep=()
for cask in ${(k)installed_casks_map}; do
    if [[ -z "${file_casks_map[$cask]}" ]]; then
        echo_space

        echo "Cask '$cask' discrepancy found. What would you like to do?"
        echo "1) Use 'soap'"
        echo "2) Uninstall"
        echo "3) Keep"
        echo_space

        read -k choice
        echo_space

        case $choice in
            1)
                soap "$cask"
                ;;
            2)
                brew uninstall "$cask"
                ;;
            3)
                casks_to_keep+=($cask)
                ;;
            *)
                casks_to_keep+=($cask)
                ;;
        esac
        echo_space
    fi
done

if [[ ${#casks_to_keep[@]} -gt 0 ]]; then
    echo_info "Kept casks listed"
else
    echo_success "No discrepancies found"
fi

echo_space
echo_final_success "Cleanup Process Completed"
