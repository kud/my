#!/usr/bin/env zsh

# Load helper functions and variables
source $MY/core/helper

### CASKS CHECK ###

# Paths where the casks installation list is maintained
file_paths=("$MY/core/brew" "$MY/core/fonts" "$MY/profiles/$OS_PROFILE/core/brew")

# Associative array to store the casks from the files
declare -A file_casks_map

# Loop through each file path to extract the casks
for file_path in "${file_paths[@]}"; do
    while read -r line || [[ -n "$line" ]]; do
        # Strip out comments from the line
        line_without_comment=${line%%#*}

        # Remove any extra spaces or tabs
        trimmed_line=$(echo "$line_without_comment" | xargs)

        # Extract cask name if it's prefixed with a path
        if [[ "$trimmed_line" =~ ^caskinstall\ (.+)$ ]]; then
            cask_name=${match[1]}
            sanitized_cask_name=$(echo "$cask_name" | awk -F/ '{print $NF}')
            file_casks_map[$sanitized_cask_name]=1
        fi
    done <"$file_path"
done

# Get a list of all installed casks
installed_casks=$(brew list --cask)

# Map to store the installed casks
declare -A installed_casks_map
for cask in ${(f)installed_casks}; do
    installed_casks_map[$cask]=1
done

# Check which casks are mentioned in files but not installed
missing_casks=()
for cask in ${(k)file_casks_map}; do
    if [[ -z "${installed_casks_map[$cask]}" ]]; then
        missing_casks+=($cask)
    fi
done

if [[ ${#missing_casks[@]} -gt 0 ]]; then
    echo_info "Casks in files but not installed:"
    for cask in "${missing_casks[@]}"; do
        echo "$cask"
    done
    exit 1
fi

echo_space
echo_success "All casks in files are installed."
echo_space

# Handle discrepancies: casks installed but not listed in files
casks_to_keep=()
for cask in ${(k)installed_casks_map}; do
    if [[ -z "${file_casks_map[$cask]}" ]]; then
        echo_space
        echo_info "Cask '$cask' is installed but not in files."

        # User prompt to handle this discrepancy
        echo_space
        echo "What would you like to do?"
        echo "1) Use 'soap'"
        echo "2) Use 'brew uninstall'"
        echo "3) Keep and list at the end"
        echo_space

        read -k choice
        echo_space

        case $choice in
            1)
                soap "$cask"
                ;;
            2)
                brew uninstall "$cask"
                ;;
            3)
                casks_to_keep+=($cask)
                ;;
            *)
                echo "Invalid choice. Keeping '$cask' for now."
                casks_to_keep+=($cask)
                ;;
        esac
    fi
done

if [[ ${#casks_to_keep[@]} -gt 0 ]]; then
    echo_space
    echo_info "Casks you chose to keep:"
    for cask in "${casks_to_keep[@]}"; do
        echo "$cask"
    done
else
    echo_info "No casks kept."
fi
